plugins {
    id 'java'
    id "application"

    // Hibernate
    id "org.hibernate.orm" version "6.1.3.Final"

    // Jacoco coverage
    id "jacoco"
}

group '3900-M10C-tickr'
version '1.0'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenCentral()
}

mainClassName = "tickr.Main"

dependencies {
    // Log4J
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'

    // SLF4J
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'

    // Spark
    implementation "com.sparkjava:spark-core:2.9.4"

    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    // H2 (testing)
    testRuntimeOnly 'com.h2database:h2:2.1.214'

    // Gson
    implementation 'com.google.code.gson:gson:2.9.1'

    // MySQL
    runtimeOnly "mysql:mysql-connector-java:8.0.30"

    // JJWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.11.5'
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("coverage")
        xml.outputLocation = layout.buildDirectory.file("coverage/report.xml")
    }
}

run {
    standardInput = System.in
}